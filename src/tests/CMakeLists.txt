if(VERBOSE)
	message(STATUS "Enabling Testing")
endif()

set(PRT_Pre Test.h testscene.inl)

FUNCTION(PUSH_TEST name files)
	set(options USES_LOADER NO_ADD)
	set(oneValueArgs)
	set(multiValueArgs)
	cmake_parse_arguments(__args "${options}" "${oneValueArgs}" "${multiValueArgs}" ${ARGN} )

	list(APPEND files ${PRT_Pre})
	set(TARGET pr_test_${name})
	add_executable(${TARGET} ${files})
	target_link_libraries(${TARGET} PRIVATE pr_lib_core)
	if(__args_USES_LOADER)
		target_link_libraries(${TARGET} PRIVATE pr_lib_loader)
	endif()
	target_compile_definitions(${TARGET} PRIVATE "${DEFS}" "$<$<CONFIG:Debug>:${DEFS_DEBUG}>")
	add_lto(${TARGET})
	strip_binary(${TARGET})
	if(NOT __args_NO_ADD)
		add_test(NAME ${name} COMMAND ${TARGET})
	endif()
ENDFUNCTION(PUSH_TEST)

PUSH_TEST(plane plane.cpp)
PUSH_TEST(entity entity.cpp)
PUSH_TEST(boundingbox boundingbox.cpp)
PUSH_TEST(projection projection.cpp)
PUSH_TEST(brdf brdf.cpp)
PUSH_TEST(sphere sphere.cpp)
PUSH_TEST(generator generator.cpp)
PUSH_TEST(triangle triangle.cpp)
PUSH_TEST(lpe lpe.cpp)
PUSH_TEST(fresnel fresnel.cpp)
PUSH_TEST(quicksort quicksort.cpp)
PUSH_TEST(radixsort radixsort.cpp)
PUSH_TEST(random random.cpp)
PUSH_TEST(kdtree kdtree.cpp)
PUSH_TEST(simd simd.cpp)
PUSH_TEST(compression compression.cpp)
PUSH_TEST(tangent tangent.cpp)
PUSH_TEST(vector vector.cpp)
PUSH_TEST(curve curve.cpp)
PUSH_TEST(quadric quadric.cpp)
PUSH_TEST(parameter parameter.cpp USES_LOADER)
PUSH_TEST(memory memory.cpp)
PUSH_TEST(upsampler upsampler.cpp USES_LOADER)

if(PR_HAS_PYTHON_API AND PR_BUILD_TESTS_PYTHON)
	add_subdirectory(python)
endif()